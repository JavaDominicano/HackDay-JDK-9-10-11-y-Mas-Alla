plugins {
    id 'java-library'
}

dependencies {
    implementation 'com.google.guava:guava:22.0'

    testImplementation 'junit:junit:4.12'
}

// tag::compileJava[]
ext.moduleName = 'org.gradle.actors' // <1>

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath,
        ]
        classpath = files()  // <2>
    }
}
// end::compileJava[]

// tag::compileTestJava[]
compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath, // <1>
            '--add-modules', 'junit',  // <2>
            '--add-reads', "$moduleName=junit", // <3>
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath, // <4>
        ]
        classpath = files()
    }
}
// end::compileTestJava[]

// tag::test[]
test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
            '--module-path', classpath.asPath, // <1>
            '--add-modules', 'ALL-MODULE-PATH', // <2>
            '--add-reads', "$moduleName=junit", // <3>
            '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath, // <4>
        ]
        classpath = files()
    }
}
// end::test[]
